import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.mikepenz.aboutlibraries.plugin'

android {
    compileSdk 35

    def localPropertiesFile = rootProject.file('local.properties')
    def localProperties = new Properties()
    if (localPropertiesFile.exists()) {
        localProperties.load(localPropertiesFile.newDataInputStream())
    }

    def posthogApiKey = localProperties.getProperty("POSTHOG_API_KEY", POSTHOG_API_KEY)
    def host = localProperties.getProperty("HOST", HOST)
    def serverPosthog = localProperties.getProperty("SERVER_POSTHOG", SERVER_POSTHOG)
    def serverRoot = localProperties.getProperty("SERVER_ROOT", SERVER_ROOT)
    def serverMelior = localProperties.getProperty("SERVER_MELIOR", SERVER_MELIOR)
    def serverS3 = localProperties.getProperty("SERVER_S3", SERVER_S3)
    def appId = localProperties.getProperty("APP_ID", APP_ID)
    def appName = localProperties.getProperty("APP_NAME", APP_NAME)

    defaultConfig {
        minSdkVersion 23
        targetSdk 35
        versionCode 4_12_01
        versionName "4.12.1"
        applicationId appId

        buildConfigField "String", "POSTHOG_API_KEY", "\"$posthogApiKey\""

        buildConfigField "String", "HOST", "\"$host\""
        buildConfigField "String", "SERVER_POSTHOG", "\"$serverPosthog\""
        buildConfigField "String", "SERVER_ROOT", "\"$serverRoot\""
        buildConfigField "String", "SERVER_MELIOR", "\"$serverMelior\""
        buildConfigField "String", "SERVER_S3", "\"$serverS3\""
    }

    buildTypes {
        release {
            resValue "string", "app_name", appName
            manifestPlaceholders = [crashlyticsEnabled: false, usesCleartextTraffic: false, host: HOST]
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            resValue "string", "app_name", appName
            manifestPlaceholders = [crashlyticsEnabled: false, usesCleartextTraffic: true, host: HOST]
            applicationIdSuffix ".debug"
        }
    }
    flavorDimensions "version"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    namespace 'com.dzen.campfire'
}

dependencies {
    implementation project(':CampfireSDK')
    implementation "com.posthog:posthog-android:3.2.1"

    implementation "io.coil-kt:coil-gif:2.6.0"
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

aboutLibraries {
    configPath = "config"
}
